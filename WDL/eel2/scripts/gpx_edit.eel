// does not work for all .gpx files
// writes to output on every mouseup after edit
// move points with mouse, alt+click to delete
// click/drag to pan
// mousewheel to zoom

tab = 10000;
tabsz = 0;

is_dirty = 0;
circle_size = 5;

function scale(v) global() ( (v - this.min) / (this.max-this.min) );
function unscale(v) global() ( v * (this.max-this.min) + this.min);
function zoom(sc) global() local(c h) (
  h = (this.max - this.min) * sc;
  c = (this.max + this.min) * .5;
  this.max = c + h*.5;
  this.min = c - h*.5;
);
function include(v) global() ( this.min = min(v,this.min); this.max = max(v,this.max); );

function scroll(amt) global() ( amt *= (this.max-this.min); this.max += amt; this.min += amt; );
function zoom_view(sc) ( v_lon.zoom(sc); v_lat.zoom(sc); );
function scroll_view(dx, dy) ( v_lat.scroll(-dy/gfx_h); v_lon.scroll(-dx/gfx_w); );

function hit_test(x,y) global(tab tabsz circle_size gfx_w gfx_h v_lat.scale v_lon.scale) local(p hit) (
  p = 0;
  hit = -1;
  while (p < tabsz && hit < 0) (
    sqr(v_lat.scale(tab[p*2+1])*gfx_h-y)+sqr(v_lon.scale(tab[p*2])*gfx_w-x) < circle_size*circle_size ? hit = p;
    p += 1;
  );
  hit
);

function do_file(srcfn, destfn) local(fp fpout p lat lon skipping) (
  (fp = fopen(srcfn,"r")) > 0 ? (
    destfn < 0 || (fpout = fopen(destfn,"w")) > 0 ? (
      p = tab;
      skipping = 0;
      while (fgets(fp,#line)) (
        match("%s<trkpt lat=\"%f\" lon=\"%f\">%s",#line,#lead,lat,lon,#trail) ? (
          destfn < 0 ? ( tabsz+=1; v_lat.include(p[1] = -lat); v_lon.include(p[0] = lon);) : 
            ( !(skipping = p[0] == 10000) ? fprintf(fpout,"%s<trkpt lat=\"%.7f\" lon=\"%.7f\">%s",#lead,-p[1],p[0],#trail); );
          p += 2;
        ) : destfn >= 0 ? ( skipping ? ( match("*</trkpt>*",#line) ? skipping = 0; ) : fwrite(fpout,#line,0) );
      );
      destfn >= 0 ? fclose(fpout);
    );
    fclose(fp);
  );
);


function run() (
  mouse_wheel ? ( zoom_view(mouse_wheel < 0 ? 1.1 : 1/1.1); mouse_wheel = 0;);
  (mouse_cap&1)? (
    !(last_mouse_cap & 1) ? (
      cap_mode = hit_test(mouse_x,mouse_y);
    ) : cap_mode >= 0 ? (
      tab[cap_mode*2] = v_lon.unscale(mouse_x/gfx_w);
      tab[cap_mode*2+1] = v_lat.unscale(mouse_y/gfx_h);
      is_dirty = 1;
    ) : scroll_view(mouse_x-last_mouse_x,mouse_y-last_mouse_y);
  ) : (
    (last_mouse_cap & 1) && (last_mouse_cap & 16) && cap_mode >= 0 ? (
      tab[cap_mode*2] = 10000;
      is_dirty = 1;
    );
    is_dirty ? ( do_file(argv[1], argv[2]); is_dirty = 0; );
  );
  last_mouse_cap = mouse_cap;
  last_mouse_x = mouse_x;
  last_mouse_y = mouse_y;

  p = tab;
  hadprev = dist = 0;
  loop(tabsz,
    p[0] != 10000 ? (
      x = v_lon.scale(p[0])*gfx_w;
      y = v_lat.scale(p[1])*gfx_h;
      hadprev ? (
        gfx_set(0,.5,.7);
        gfx_line(v_lon.scale(llon)*gfx_w,v_lat.scale(llat)*gfx_h,x,y);
        dist += sqrt(sqr((p[0] - llon) * cos(llat*$pi/180.0)*69.172) + sqr((p[1] - llat)*60));
      );
      gfx_set(0,1,0);
      gfx_circle(x,y,circle_size);
      llon = p[0];
      llat = p[1];
      hadprev = 1;
    );
    p += 2;
  );

  gfx_x=gfx_y=0;
  gfx_set(1,1,0);
  gfx_printf("distance (approx) %.2f mi\n",dist);

  gfx_update();
  c = gfx_getchar();
  c >= 0 && c != 27 ? defer("run()");
);

argc < 2 ? printf("Usage: %s file.gpx output.gpx\n",argv[0]) : (
  v_lat.min = v_lon.min = 100000;
  v_lat.max = v_lon.max = -100000;
  do_file(argv[1],-1);
  tabsz > 0 ? (
    zoom_view(1.3);
    gfx_init("gpx edit",1024,768);
    defer("run()");
  ) : printf("Error: %s has no <trkpt lines matching\n",argv[1]);
);

